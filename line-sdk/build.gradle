apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "com.linecorp.linesdk"
version = "5.7.0"

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 5_07_00
        versionName version

        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""
    }

    dataBinding {
        enabled = true
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = "line-sdk-${defaultConfig.versionName}"
            if (buildType.name == 'debug') {
                fileName += '-debug'
            }
            fileName += '.aar'
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        lintConfig file("lint.xml")
        abortOnError false
    }
}

configurations {
    doclava
    javadocs
}

dependencies {
    javadocs "androidx.annotation:annotation:${ver.androidx.annotation}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${ver.kotlin}"

    implementation "androidx.annotation:annotation:${ver.androidx.annotation}"
    implementation "androidx.appcompat:appcompat:${ver.androidx.appcompat}"
    implementation "androidx.browser:browser:${ver.androidx.browser}"
    implementation "androidx.constraintlayout:constraintlayout:${ver.androidx.constraintlayout}"
    implementation "androidx.core:core-ktx:${ver.androidx.core}"
    implementation "androidx.exifinterface:exifinterface:${ver.androidx.exifinterface}"
    implementation "androidx.legacy:legacy-support-v4:${ver.androidx.legacy}"
    implementation "androidx.lifecycle:lifecycle-extensions:${ver.androidx.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${ver.androidx.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${ver.androidx.lifecycle}"

    // JSON Web Token
    api "io.jsonwebtoken:jjwt-api:${ver.jjwt}"
    implementation "io.jsonwebtoken:jjwt-impl:${ver.jjwt}"
    implementation("io.jsonwebtoken:jjwt-orgjson:${ver.jjwt}") {
        exclude group: 'org.json', module: 'json' //provided by Android natively
    }

    implementation "com.google.android.material:material:${ver.google.material}"
    implementation("com.madgag.spongycastle:prov:${ver.spongycastle}") {
        exclude group: 'junit', module: 'junit'
    }
    implementation "com.squareup.picasso:picasso:${ver.picasso}"
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // for tests
    testImplementation "junit:junit:${ver.jUnit}"
    testImplementation "org.mockito:mockito-inline:${ver.mockito}"
    testImplementation "org.robolectric:robolectric:${ver.robolectric}"

    // for @hide annotation on functions to hide it from javadoc
    doclava "com.google.doclava:doclava:${ver.doclava}"
}

ext {
    javadocParams = [
            taskGroup    : "javadoc",
            title        : "LINE SDK for Android",
            locale       : "en_US",
            encoding     : "UTF-8",
            source       : android.sourceSets.main.java.srcDirs,
            classpath    : project.files(android.getBootClasspath().join(File.pathSeparator)),
            destDir      : [
                    public  : new File(project.buildDir, "docs/javadoc_public"),
                    internal: new File(project.buildDir, "docs/javadocs")
            ],
            includes     : [
                    public  : [
                            "**/com/linecorp/linesdk/*.java",
                            "**/com/linecorp/linesdk/api/*.java",
                            "**/com/linecorp/linesdk/auth/*.java",
                            "**/com/linecorp/linesdk/widget/*.java"
                    ],
                    internal: [
                            "**/com/linecorp/linesdk/*.java",
                            "**/com/linecorp/linesdk/api/*.java",
                            "**/com/linecorp/linesdk/auth/*.java",
                            "**/com/linecorp/linesdk/widget/*.java",
                            "**/com/linecorp/linesdk/message/**/*.java"
                    ]
            ],
            externalLinks: [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://developer.android.com/reference/"
            ]
    ]

    bintrayRepo = 'line-sdk'
    bintrayName = 'com.linecorp.linesdk'

    // Library Details
    publishedGroupId = 'com.linecorp.linesdk'
    libraryName = 'com.linecorp.linesdk'
    artifact = 'linesdk'
    libraryDescription = 'The LINE SDK for Android provides a modern way of implementing LINE APIs.'
    libraryVersion = version

    // Git Details
    siteUrl = 'https://github.com/line/line-sdk-android'
    gitUrl = 'https://github.com/line/line-sdk-android'
    githubRepository= 'line/line-sdk-android'

    // Developer Details
    developerId = 'line'
    developerName = 'line'
    developerEmail = 'dl_oss_dev@linecorp.com'

    // License Details
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId


task javadoc_public(type: Javadoc) {
    group javadocParams.taskGroup
    destinationDir = javadocParams.destDir.public

    // doclava doesn't support `title`
    title = null

    source = javadocParams.source
    classpath += javadocParams.classpath
    classpath += configurations.compile
    classpath += configurations.javadocs
    include javadocParams.includes.public

    failOnError false

    doFirst {
        options {
            doclet 'com.google.doclava.Doclava'
            docletpath = configurations.doclava.files.asType(List)

            exclude '**/BuildConfig.java'
            exclude '**/R.java'

            locale javadocParams.locale
            encoding javadocParams.encoding

            // doclava supports `-title ${title}`
            addStringOption("title", "${javadocParams.title}")
            addStringOption("templatedir", "${rootDir}/templates")
            addMultilineMultiValueOption("federate").with {
                javadocParams.externalLinks.each {
                    value.add(["name", it])
                }
            }
            addMultilineMultiValueOption("hdf").with {
                value.add(["project.version", android.defaultConfig.versionName])
            }
        }
    }
}

task javadoc(type: Javadoc) {
    group javadocParams.taskGroup
    destinationDir = javadocParams.destDir.internal

    source = javadocParams.source
    classpath += javadocParams.classpath
    include javadocParams.includes.internal

    failOnError false

    options {
        docTitle "${javadocParams.title}"
        windowTitle "${javadocParams.title}"

        locale javadocParams.locale
        encoding javadocParams.encoding

        links = javadocParams.externalLinks
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId 'linesdk'
                version version

                from(components["release"])
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'linesdk'
                    description = 'The LINE SDK for Android provides a modern way of implementing LINE APIs.'
                    url = 'https://github.com/line/line-sdk-android'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'LINE Corporation'
                            email = 'dl_oss_dev@linecorp.com'
                            url = 'https://engineering.linecorp.com/en/'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:line/line-sdk-android.git'
                        developerConnection = 'scm:git:ssh://github.com:line/line-sdk-android.git'
                        url = 'https://github.com/line/line-sdk-android'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                def name = findProperty('repositoryUsername') ?: ''
                def pw = findProperty('repositoryPassword') ?: ''
                credentials {
                    username name
                    password pw
                }
            }
        }
    }

    signing {
        def key = findProperty('signingKey')
        def pw = findProperty('signingPassword')
        required { signingKey != null && signingPassword != null }
        useInMemoryPgpKeys(key, pw)
        sign publishing.publications
    }
}

nexusStaging {
    packageGroup = 'com.linecorp'
    def name = findProperty('nexusUsername') ?: ''
    def pw = findProperty('nexusPassword') ?: ''
    username = name
    password = pw
}



